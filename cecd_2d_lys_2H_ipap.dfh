;
; Filename: cecd_2d_lys_2H_ipap.dfh 
;
; This sequence performs the following experiment
;   CeCd-SQC carbon-carbon correlation for lysine side-chains and
;   CdCg-SQC carbon-carbon correlation for arginine side-chains
;   Selection with IPAP 
;     . Default is inphase 
;     . -DSELECT_AP selects antiphase during last INEPT
;
; Samples should be U-13C2H labelled
;   If the sample is 15N labelled then remember to turn on the decoupler
;   during acquisition
;
; Set the following parameters
;   O1P    to the average chemical shift of lys(Ce) Arg(Cd) ~ 42 ppm
;   CNST22 to the average chemical shift of lys(Cd) Arg(Cg) ~ 29 ppm
;   CNST2  to scalar 13C-13C coupling (36 Hz)
;   pl17 (deuterium decoupling) to approx 850 Hz
;
; The following selective pulses should be checked
;   - Selective excitation (and time-reverse) of Lys Ce/Arg Cd with 
;     EBurp2 (p14@sp12, p14@sp14)
;   - Selective Lys Ce/Arg Cd refocusing ReBurp (p15@sp16)
;   - Selective Lys Cd/Arg Cg refocusing ReBurp (p15@sp15)
;   - 2H decoupling during indirect evolution and Cd <-> Ce INEPTS 
;     at a power of pl17
;   - 2H flanking pulses p51@pl17
;
;   Written by D. Flemming Hansen on Sep 18th 2013


;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

prosol relations=<triple_arg>

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>


"d11=30m"
"d12=20u"
"d13=4u"

"in0=inf1/2"
"in10=in0"

"p2=p1*2"
"d4=1s/(cnst2*4)"
"d5=1s/cnst2"

"d0=0.5*d5-0.5*p2-d12"
"d10=d0"

"DELTA1=d4-d12-0.5*p2"
"DELTA3=d4-d12-0.5*p2-d13-p51"


"cnst21=o1/bf1"


1 ze 
  d11 LOCKDEC_ON
  50u LOCKH_ON
  d11 H2_PULSE
  d12 pl26:f3 pl17:f4
2 d11 do:f3 do:f4
  d11 H2_LOCK
  6m LOCKH_OFF
  d1
  50u LOCKH_ON
  d12 H2_PULSE
  d12 pl0:f1 pl17:f4
  30u fq=cnst21(bf ppm):f1
  50u UNBLKGRAMP
;
; Start deuterium decoupling
  (p51 ph3):f4
  d13
  d13 cpd4:f4 ph2
;
  (p14:sp12 ph1):f1
  d12 pl1:f1
  DELTA1
  (p2 ph2):f1
  DELTA1
  d12 pl0:f1
  (p14:sp14 ph3):f1
;
; Stop deuterium decoupling
  d13 do:f4
  (p51 ph6):f4
;
  30u fq=cnst22(bf ppm):f1
  d12 pl0:f1
;  GRADIENT START
  p16:gp1
  d16
; GRADIENT STOP
;
; Start deuterium decoupling
  (p51 ph3):f4
  d13
  d13 cpd4:f4 ph2
;
; Start constant-time evolution
  (p14:sp12 ph4):f1
  d12 pl1:f1
  d0
  (p2 ph2):f1  
  d12 pl0:f1
  d10
  (p14:sp14 ph2):f1
; End constant-time evolution
; Stop deuterium decoupling
  d13 do:f4
  (p51 ph6):f4
  d12
;
;
; GRADIENT START
  p16:gp2
  d16
; GRADIENT STOP
  30u fq=0:f1 pl0:f1
  d13 BLKGRAMP
;
; Start deuterium decoupling (this will be kept on during acq)
  (p51 ph3):f4
  d13
  d13 cpd4:f4 ph2
;
  (p14:sp12 ph2):f1
#ifdef SELECT_AP
  d12 pl0:f1
  DELTA1
  (p15:sp15 ph2):f1
  (p15:sp16 ph2):f1
  (p15:sp15 ph2):f1
  DELTA1
  d12 pl0:f1
#else
  d12 pl0:f1
  DELTA1
  (p15:sp15 ph2):f1
  (p15:sp16 ph2):f1
  DELTA1
  (p15:sp15 ph2):f1
  d12 pl0:f1 pl26:f3
#endif
;
; ACQUISITION
  go=2 ph31 cpd3:f3
  d11 do:f3 do:f4 mc #0 to 2
	F1PH(ip4, id0 & dd10)
;             |- ph4 is the states-tppi phase
;                  | increment delay d0
;                         | decrement delay d10


  d11 H2_LOCK
  d11 LOCKH_OFF
  d11 LOCKDEC_OFF

;so we can loop to the end.. 
stop, exit


ph1= 0 0 2 2 
ph2= 0
ph3= 1
ph4= 0 2
;ph5= 0 0 2 2 
ph6= 3
ph31=0 2 2 0


;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl4 : f4 channel - power level for pulse (default)
;pl17: f4 channel - power level for CPD/BB decoupling
;pl26: f3 channel - power level for CPD/BB decoupling (low power)
;sp12 : f1 channel - shaped pulse  90 degree
;sp13 : f1 channel - shaped pulse 180 degree (adiabatic)
;spnam12 : Eburp2.1000
;sp21 : f3 channel - shaped pulse 180 degree
;spnam21 : Reburp.1000
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p8 : f1 channel - 180 degree shaped pulse for inversion (adiabatic)
;p14 : f1 channel - 90 degree shaped pulse  [1.5 ms]
;p16: homospoil/gradient pulse
;p21 : f3 channel -  90 degree high power pulse
;p23 : f3 channel - 180 degree shaped pulse  [6 ms]
;p51 : f4 channel -  90 degree pulse at pl17
;d0 : incremented delay (2D)                         [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J)XH
;d10: Constant time-delay ( 22ms )
;d11: delay for disk I/O                             [30 msec]
;d12: delay for power switching                      [20 usec]
;d13: short delay                                    [4 usec]
;d16: delay for homospoil/gradient recovery
;cnst2: = J(CC)  [40 Hz]
;cnst21: C_epsilon chemical shift offset      [= o1p, 156 ppm]
;cnst22: C_delta chemical shift offset        [40.5 ppm]
;o1p: C_epsilon chemical shift offset         [156 ppm]
;o2p: H_epsilon/H_delta chemical shift offset [7 ppm]
;o3p: N_eta chemical shift offset             [35.0 ppm]
;o4p: H_epsilon/H_delta chemical shift offset [3.5 ppm]
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;NS: 8 * n
;DS: 16
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;cpd3:   decoupling according to sequence defined by cpdprg3: garp4.p62
;p62:    f3 channel - 90 degree pulse for decoupling sequence [350 us]
;cpd4:   decoupling according to sequence defined by cpdprg4
;pcpd4:  f4 channel - 90 degree pulse for decoupling sequence

;for z-only gradients:
;gpz1: 7 %
;gpz2: 17 %
;gpz3: 33 %
;gpz4: 13 %

;use gradient files:
;gpnam1: SINE.100
;gpnam2: SINE.100

;preprocessor-flags-start
;HALFDWELL: for initial sampling delay of half a dwell-time with
;           option -DHALFDWELL (eda: ZGOPTNS)
                                          ;preprocessor-flags-end

