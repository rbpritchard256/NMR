;
; Filename: cecd_2d_lys_2H_ipap.dfh 
;
; This sequence performs the following experiment
;   CeCd-SQC carbon-carbon correlation for lysine side-chains and
;   CdCg-SQC carbon-carbon correlation for arginine side-chains
;;
; Samples should be U-13C2H labelled
;   If the sample is 15N labelled decoupling of 15N should be carried out
;   in the direct dimension, however, current cold probes cannot do
;   15N decoupling and 2H decoupling during 13C acquisition: D+N=C
;   during acquisition
;
; Set the following parameters
;   O1P    to the average chemical shift of lys(Ce) Arg(Cd) ~ 42 ppm
;   CNST22 to the average chemical shift of lys(Cd) Arg(Cg) ~ 29 ppm
;   CNST2  to scalar 13C-13C coupling (36 Hz)
;
; The following selective pulses should be checked
;   - Selective excitation (and time-reverse) of Lys Ce/Arg Cd with 
;     EBurp2 (p14@sp12, p14@sp14)
;   - Selective Lys Ce/Arg Cd refocusing ReBurp (p15@sp16)
;   - Selective Lys Cd/Arg Cg refocusing ReBurp (p15@sp15)
;   - 2H decoupling during indirect evolution and Cd <-> Ce INEPTS 
;     at a power of pl17
;   - 2H flanking pulses p51@pl17
;
;   Written by D. Flemming Hansen on Sep 18th 2013


;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>


"d11=30m"
"d12=15u"
"d13=15u"

define pulse pwc
       "pwc=p1"

define pulse pwc90_sel1
       "pwc90_sel1=p11"

define pulse pwc90_sel2
        "pwc90_sel2=p13"

define pulse pwc180_sel1
       "pwc180_sel1=p15"

define pulse pwc180_sel2
       "pwc180_sel2=p16"

define pulse pwc_dip
       "pwc_dip=p17"

define pulse pwd
       "pwd=p41"

define delay taua
       "taua=1s/(cnst2*4.)"

define delay TC
       "TC=1s/(cnst2*1.)"

"in0=inf1/2"
#ifdef CT
  "d0=0.1u"
#else
  "d0=in0/2.-0.6366*pwc-d12-2u"
#endif

define delay flopsytime

define list<loopcounter> ap_flg = {0 1}
define loopcounter maxtd
"l11=0"

"maxtd= 2.*trunc( ( TC*0.5 - pwc - 4u - d12 - d0)/in0)"
"flopsytime=188.448*p17*l1"

"cnst21=o1/bf1"

;hard code zero power
"plw0=0."

;hard code offsets and spoals
"spoal11=1"
"spoffs11=0."

"spoal12=0."
"spoffs12=0."

"spoal13=1."
"spoffs13=0."

"spoal14=0."
"spoffs14=0."

"spoffs15=0."
"spoffs16=(cnst22-cnst21)*bf1/1000000."

;calculate power levels 
#ifdef AUTOCAL
   "spw11=(pwc/pwc90_sel1)*(pwc/pwc90_sel1)*plw1/(0.05813*0.05813)"
   "spw12=(pwc/pwc90_sel1)*(pwc/pwc90_sel1)*plw1/(0.05813*0.05813)"
   "spw13=(pwc/pwc90_sel2)*(pwc/pwc90_sel2)*plw1/(0.05813*0.05813)"
   "spw14=(pwc/pwc90_sel2)*(pwc/pwc90_sel2)*plw1/(0.05813*0.05813)"
   "spw15=(pwc/pwc180_sel1)*(pwc/pwc180_sel1)*plw1/(0.041920*0.041920)"
   "spw16=(pwc/pwc180_sel2)*(pwc/pwc180_sel2)*plw1/(0.041920*0.041920)"
#endif


;define constants to check power levels
"cnst41=plw41"
"acqt0=0.0"

1 ze
  if "td1>maxtd" 
  {
    10u
    print "ERROR: TD1 too large"
    goto HaltAcqu
  }
  if "flopsytime>19m"
  {
    10u
    print "ERROR: FLOPSY mixing too long"
    goto HaltAcqu
  }
  if "pwc_dip < 32u"
  {
     10u
     print "ERROR: FLOPSY strength too high"
     goto HaltAcqu
  }
  if "pwd<125u"
  {
    10u
    print "ERROR: Deuterium decoupling power too high"
    goto HaltAcqu
  }
  if "cnst41>20.0"
  {
    10u
    print "ERROR: Deuterium decoupling power too high"
    goto HaltAcqu
  }

  d11 LOCKDEC_ON
  50u LOCKH_ON
  d11 H2_PULSE
  d12 pl31:f3 pl41:f4
2 d11 do:f3 do:f4
  d11 H2_LOCK
;
; wait for the lock to stabilise before starting to lock
  d1*0.33
  6m LOCKH_OFF
  d1*0.67
  50u LOCKH_ON
  d12 H2_PULSE

  d12 pl0:f1 pl41:f4
  30u fq=cnst21(bf ppm):f1
  50u UNBLKGRAMP
  30u fq=0:f4
  30u fq=0:f3
  d12 pl31:f3
;
; Start deuterium decoupling
  (p41 ph11):f4
  d13
  d13 cpd4:f4 ph2
;
  (pwc90_sel1:sp11 ph1):f1
  4u
  d12 pl0:f1
  vd
  (pwc180_sel1:sp15 ph11):f1
  4u
  d12 pl0:f1
  vd
  d12 pl1:f1
  4u
  "DELTA=taua-4u-d12-pwc"
  DELTA
  (pwc*2 ph10):f1
  "DELTA=taua-pwc-d12-4u"
  DELTA
  d12 pl0:f1
  4u
  (pwc90_sel1:sp12 ph11):f1
;
; Stop deuterium decoupling
  d13 do:f4
  (p41 ph13):f4
;
  30u fq=cnst22(bf ppm):f1
  d12 pl0:f1
  4u
;  GRADIENT START
  p51:gp1
  d16
; GRADIENT STOP
;
; Start deuterium decoupling
  30u fq=cnst42(bf ppm):f4
  (p41 ph11):f4
  d13
  d13 cpd4:f4 ph2
;
; Chemical shift evolution
  (pwc90_sel2:sp13 ph4):f1
#ifdef CT
;
; Start constant-time evolution
  4u
  d12 pl1:f1
  "DELTA=TC*0.5+d0-4u-d12-pwc"
  DELTA
  (pwc*2 ph2):f1
  4u
  d12 pl0:f1
  "DELTA=TC*0.5-d0-pwc-4u-d12"
  DELTA
#else
;
; Start non-constant-time evolution
  4u
  d12 pl0:f1
  d0
  d0
  d12 
#endif
  (pwc90_sel2:sp14 ph2):f1
; End constant-time evolution
; Stop deuterium decoupling
  d13 do:f4
  (p41 ph13):f4
  d12
;
;
; GRADIENT START
  p52:gp2
  d16
; GRADIENT STOP
  30u fq=cnst21(bf ppm):f1 pl0:f1
  d13 BLKGRAMP
;
; Start deuterium decoupling (this will be kept on during acq)
  30u fq=0:f4
  (p41 ph11):f4
  d13
  d13 cpd4:f4 ph2
;
  if "ap_flg[l11]>0" {
    (pwc90_sel1:sp11 ph13):f1
    4u
    d12 pl0:f1
    "DELTA=taua-4u-d12"
    DELTA
    (pwc180_sel2:sp16 ph2):f1
    4u
    (pwc180_sel1:sp15 ph2):f1
    4u
    (pwc180_sel2:sp16 ph2):f1
    "DELTA=taua"
    DELTA
  } else {
    (pwc90_sel1:sp11 ph10):f1
    4u
    d12 pl0:f1
    "DELTA=taua-4u-d12"
    DELTA
    (pwc180_sel2:sp16 ph2):f1
    4u
    (pwc180_sel1:sp15 ph2):f1
    4u
    "DELTA=taua"
    DELTA
    (pwc180_sel2:sp16 ph2):f1
  }
;
; ACQUISITION
  go=2 ph31 cpd3:f3
  d11 do:f3 do:f4 mc #0 to 2
        F3QF(calclc(l11,1))
	F2QF(calclist(vd,1))
	F1PH(calph(ph4,+90),caldel(d0,+in0))

  d11 H2_LOCK
  d11 LOCKH_OFF
  d11 LOCKDEC_OFF

;so we can loop to the end for error messages .. 
HaltAcqu, 1m
stop, exit

ph1= 0 0 2 2 
ph2= 0
ph4= 0 2

ph10=0
ph11=1
ph12=2
ph13=3
;
;FLOPSY pulses
ph26=(720) 0
ph27=(720) 90
ph28=(720) 135
ph29=(720) 630
ph30=(720) 45

ph21=(720) 360
ph22=(720) 450
ph23=(720) 495
ph24=(720) 270
ph25=(720) 405
;
;receiver pulse
ph31=0 2 2 0


;p1: pwc
;p41: pwd
;p11: pwc90_sel1   90 degree shaped pulse for Ce
;p13: pwc90_sel2   90 degree shaped pulse for Cd
;p15: pwc180_sel1 180 degree shaped pulse for Ce
;p16: pwc180_sel2 180 degree shaped pulse for Cd
;p17: pwc_dip
;pwc90_sel1: 90 degree shaped pulse for Ce
;pwc90_sel2: 90 degree shaped pulse for Ce
;plw0: Zero power [0W]
;pl1: High power 13C [48W]
;pl4: High power 2H  [14W]
;pl41: 2H decoupling [4W]
;cnst2: = J(CC)  [35 Hz]
;cnst21: Chemical shift offset for terminal 13C [=o1p]
;cnst22: Chemical shift offset for coupled 13C   [ppm]
;cnst23: Chemical shift offset for FLOPSY     [33 ppm]
;sp11:   90 degree shaped pulse for Ct   [Eburp2.1000]
;spnam11:                                [Eburp2.1000]
;sp12:   90 time-reversed shaped pulse [Eburp2tr.1000]
;spnam12:                              [Eburp2tr.1000]
;sp13:   90 degree shaped pulse for Cc   [Eburp2.1000]
;sp14:   90 time-reversed shaped pulse [Eburp2tr.1000]
;sp15:  180 inversion pulse for Ct       [Reburp.1000]
;sp16:  180 inversion pulse for Cc       [Reburp.1000]
;o1p: C_epsilon chemical shift offset            [ppm]
;o2p: H_e/H_d chemical shift offset              [ppm]
;o3p: N_e chemical shift offset                  [ppm]
;o4p: H_e/H_d chemical shift offset              [ppm]
;d16: delay for homospoil/gradient recovery     [200u]
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;NS: 4 * n
;DS: 32
;l1: Number of FLOPSY loops (Check flopsytime)
;l11: IPAP counter
;ap_flg: Flag for selecting in-phase or anti-phase
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;cpd4:   decoupling according to sequence defined by cpdprg4   [CWP]
;pcpd4:  f4 channel - 90 degree pulse for decoupling sequence [250u]

;for z-only gradients:
;gpz1: 7 %
;gpz2: 17 %
;
;use gradient files:
;gpnam1: SINE.100
;gpnam2: SINE.100

;preprocessor-flags-start


